#!/bin/csh -f
#
# This sub-script applies an automatic aedit filter to the raw
# alist file in order to generate the masterlist of data to be
# analyzed.  Called from mmreduce.
#
# Written by CJL, 22 February 1996
#################################################################

					# Skip option
if (-e $1.mlist) then
    echo "mmreduce: A masterlist file already exists.  If you want"
    echo "mmreduce: to use it, you should skip this step.  If not, the existing"
    echo "mmreduce: file will be overwritten"
    skipask
    if ($status == 1) exit (0)
endif
					# Generate aedit script and give user
					# the option of manually adjusting it
cp $AESCRIPTS/mmvlbi_mlist $1.aescript
echo "write $1.mlist; exit" >> $1.aescript
while (1)
    echo "mmreduce: Preparing to construct masterlist file"
    echo "mmreduce: You have the option of editing the aedit runfile"
    echo "mmreduce: If you choose to do so, do not alter the 'write' statement"
    echo "mmreduce: Do you wish to edit the runfile? (y/n): \c"
    set reply=$<
    if ($reply == "y" || $reply == "Y") then
	$EDITOR $1.aescript
	break
    else if ($reply != "n" && $reply != "N") then
	echo "mmreduce: Please answer 'y' or 'n'"
    else
	break
    endif
end
					# Do the aedit run
aedit -r $1.aescript -f $1.alist 
if ($status != 0) then
    echo "mmreduce: Oh no!  I got an error return from aedit.  I'm quitting"
    exit (1)
endif
